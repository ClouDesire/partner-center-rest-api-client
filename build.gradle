buildscript {
    ext.kotlin_version = '1.2.10'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:latest.release"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7.3"
    id 'net.researchgate.release' version '2.6.0'
}

group 'com.cloudesire'

apply plugin: 'kotlin'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'

repositories {
    mavenCentral()
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
    kotlinOptions.includeRuntime = true
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

test {
    reports {
        junitXml.enabled = true
        html.enabled = false
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-moshi:2.3.0'

    testCompile 'junit:junit:4.12'
    testCompile 'com.winterbe:expekt:0.5.0'
    testCompile 'io.github.microutils:kotlin-logging:1.4.9'
}

test {
    reports {
        junitXml.enabled = true
        html.enabled = false
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId 'com.cloudesire'
            artifactId 'partner-center-rest-api-client'
            version version
        }
    }
}

artifactory {
    contextUrl = 'https://artifactory.cloudesire.com/artifactory'
    publish {
        contextUrl = 'https://artifactory.cloudesire.com/artifactory'
        repository {
            repoKey = 'libs-snapshot-local'
            username = "$System.env.ARTIFACTORY_USER"
            password = "$System.env.ARTIFACTORY_PASS"
        }
        defaults {
            publications ('mavenJava')
            publishBuildInfo = false
            publishArtifacts = true
            publishPom = true
        }
    }
}

artifactoryPublish {
    dependsOn jar
}

bintray {
    user = "$System.env.BINTRAY_USER"
    key = "$System.env.BINTRAY_KEY"
    publications = ['mavenJava']
    publish = true
    pkg {
        repo = 'maven-releases'
        name = 'partner-center-rest-api-client'
        userOrg = 'cloudesire'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/ClouDesire/partner-center-rest-api-client.git'
        version {
            gpg {
                sign = false
            }
        }
    }
}

bintrayUpload {
    dependsOn jar
}

afterReleaseBuild.dependsOn bintrayUpload

release {
    preTagCommitMessage = '[Gradle Release Plugin][skip-ci] - pre tag commit: '
}
